
# 3. Design and verification building blocks

## 3.5 Interfaces

Конструкция интерфейса, заключена между ключевыми словами `interface...endinterfrace`, инкапсулирует коммуникацию между блоками устройства и между верификационным блоком и устройством, позволяет плавно перейти от абстрактного уровня устройства через... Инкапсулируя связь между блоками, интерфейс также облегчает повторное использование устройства.

На низшем уровне, интерфейс - именованный пакет сетей и переменных. Экземпляр интерфейса создается в устройстве и может быть соединен к портам других созданных модулей, интерфейсов и программ. Доступ к интерфейсу может быть получен через один экземпляр порта.

В добавок, преимущества интерфейса исходят от возможности хранить функциональность так же, как соединения, на самом высоком уровне. Интерфейс может иметь параметры, константы, переменные, функции и таски. В интерфейсе могут быть объявлены типы элементов, либо переданы в качестве параметров. Ссылки на включенные переменные и функции указываются относительно имени экземпляра интерфейса как члены экземпляра. Таким образом, модули, соединенные через интерфейс, могут вызвать подпрограммы интерфейса для коммуникации. С такой функциональностью, инкапсулированной в интерфейсе и изолированной от модуля, уровень абстракции протокола коммуникации может быть легко изменен выбором другого интерфейса, содержащего те же самые функции, но реализованные на другом уровне абстракции. Модули, соединенные через интерфейс не необходимости изменять.

Чтобы предоставлять информацию о направлении портам модуля и контролировать использование подпрограмм внутри конкретных модулей, предоставлена конструкция `modport`. Как следует из названия, направления рассматриваются с точки зрения модуля.

В добавок к методам-подпрограммам, интерфейс также может содержать процессы (т. е. процедуры `initial` или `always`) и продолжительные присваивания, которые полезны для системного моделирования и тестовых окружений. Это позволяет интерфейсам содержать, например, собственные проверки протокола, которые автоматически проверяют, что все модули, соединенные через интерфейс, соответствуют определенному протоколу. Другие области применения, такие как запись функционального покрытия и отчетность, проверка протокола, и `assert` также могут быть встроены в интерфейс.

Простой пример объявления интерфейса и его использования:

```systemverilog
interface simple_bus(input logic clk);  // define the if
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;
endinterface: simple_bus

module memMod(simple_bus a);  // simple_bus interface port
	logic avail;

	// when memMod is instantiated in module top, a.req is the req
	// signal in the sb_intf interface of the 'simple_bus' interface
	always @(posedge a.clk) a.gnt <= a.req & avail;
endmodule

module cpuMod(simple_bus b);  // simple_bus interface port
	// ...
endmodule

module top;
	logic clk = 0;

	simple_bus sb_intf(.clk(clk));  // instantiate the interface

	memMod mem(.a(sb_intf));  // connect interface to module instance
	cpuMod cpu(.b(sb_intf));  // connect interface to module instance

endmodule
```


# 25. Interfaces

## 25.1 General

Этот раздел описывает следующее:
- Назначение интерфейсов
- Синтаксис
- Модпорты
- Методы
- Параметризованные интерфейсы
- Виртуальные интерфейсы
- Доступ к объектам интерфейсов

## 25.2 Overview

Коммуникации между блоками в цифровой системе - критическая зона, которая имеет влияние на все от упрощения кода RTL до аппаратно-программного разделения , анализа производительности, выбора реализации шины и проверки протокола. Конструкция `interface` в SystemVerilog была создана, чтобы инкапсулировать коммуникацию между блоками, сглаживая переход от абстрактного системного уровня устройства в низкому уровню регистровых передач. 

...

Методы могут быть абстрактными, т. е., определены в одном модуле, и вызваны в другом, используя конструкции `export` и `import`. Такое может быть написано, используя имена иерархических путей, но это воспрепятствует переиспользованию, потому что имена будут зависеть от устройства. Лучше объявлять имена подпрограмм в интерфейсе и использовать локальные иерархические имена от объекта интерфейса для определения и вызова. Broadcast коммуникации моделируется с помощью `forkjoin` задач, которые могут быть определены в более чем одном модуле и могу исполняться параллельно.

## 25.3 Interface syntax

Конструкция интерфейс предоставляет новую иерархическую структуру. Она может содержать меньшие интерфейсы и может быть передана через порты.

Цель интерфейсов - инкапсулировать коммуникацию. На низком уровне, это означает - объединение переменных и сетей в интерфейсах, а также возможность наложения ограничений на порты в модпортах. Модули могут быть сделаны универсальными, так что изменяются интерфейсы. Следующий пример демонстрирует эти функции. НА высоком уровне абстракции, коммуникации могут быть совершены с помощью функций и задач. Интерфейсы могу содержать объявления подпрограмм или их прототипы, с определением внутри одного модуля и вызовом в другом.

Пример объявления интерфейса

```systemverilog
interface identifier;
	// ...
	// interface_items
	// ...
endinterface[: identifier]
```

Интерфейс может быть объявлен иерархически, как модуль, с портами или без:

```systemverilog
myinterface #(100) scalar1(), vector[9:0]();
```

В этом примере создаются 11 экземпляров интерфейса типа `myinterface`...

Интерфейсы могут быть объявлены и созданы внутри модулей, но модули в интерфейсах - нет.

### 25.3.2 Interface example using a named bundle

Простейшая форма интерфейса - сгруппированные соединения переменных или сетей. Когда интерфейс объявляется как порт, интерфейсы и сети внутри него предполагают доступ `ref` и `inout`... Следующий пример демонстрирует базовый синтаксис определения, создания и соединения интерфейса. Возможности использования интерфейса SystemVerilog могут существенно уменьшить количество необходимого для соединения портов кода.

```systemverilog
interface simple_bus;
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;
endinterface: simple_bus

module memMod(simple_bus a, input logic clk);
	logic avail;
	always @(posedge clk) a.gnt <= a.req & avail;
endmodule

module cpuMod(simple_bus b, input logic clk);
	...
endmodule

module top;
	logic clk = 0;

	simple_bus sb_intf();  // instantiate the interface

	memMod mem(sb_intf, clk);  // connect the interface to the module instance
	cpuMod cpu(.b(sb_intf), .clk(clk));  // either by position or by name
endmodule
```

В предшествующем примере, если один и тот же идентификатор, `sb_intf`, был использован как имя для интерфейса в **заголовках** модулей, тогда неявное соединение портов может быть использовано для создания экземпляров модулей в модуле верхнего уровня:

```systemverilog
module memMod(simple_bus sb_intf, input logic clk);
	...
endmodule

module cpuMod(simple_bus sb_intf, input lobic clk);
	...
endmodule

module top;
	logic clk = 0;
	simple_bus sb_intf();

	memMod mem(.*);  // implicit port connections
	cpuMod cpu(.*);  // implicit port connections

endmodule
```

### 25.3.3 Interface example using a generic bundle

Заголовок модуля может быть создан со ссылкой на неопределенный интерфейс в качестве заполнителя для интерфейса, который будет выбран при создании экземпляра самого модуля. Неопределенный интерфейс называют ссылкой на общий интерфейс.

Ссылка на общий интерфейс может быть объявлена используя синтаксический стиль ANSI.

Следующий пример показывает, как указать общий интерфейс в определении модуля:

```systemverilog
// memMod and cpuMod can use any interface
module memMod(interface a, input logic clk);
	...
endmodule

module cpuMod(interface b, input logic clk);
	...
endmodule

interface simple_bus;  // define the interface
	logic reg, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;
endinterface: simple_bus

module top;
	logic clk = 0;
	simple_bus sb_intf();  // instantiate the interface

	// reference the sb_intf instance of the simple_bus
	// interface from the generic interfaces of the
	// memMod and cpuMod modules
	memMod mem(.a(sb_intf), .clk(clk));
	cpuMod cpu(.b(sb_intf), .clk(clk));

endmodule
```

Неявное портирование не может быть использована, чтобы сослаться на общий интерфейс. Следует использовать поименное портирование:

```systemverilog
module memMod(interface a, input logic clk);
	...
endmodule

module cpuMod(interface b, input logic clk);
	...
endmodule

module top;
	logic clk = 0;
	simple_bus sb_intf();

	memMod mem(.*, .a(sb_intf));  // partial implicit port connections
	cpuMod cpu(.*, .b(sb_intf));  // partial implicit port connections

endmodule
```

## 25.4 Ports in interfaces

Одно из ограничений простых интерфейсов - сети и переменные, объявленные внутри интерфейса используются только чтобы соединять порты с теми же сетями и переменными. Для того, чтобы совместно использовать внешнюю сеть или переменную, которая устанавливает соединение извне интерфейса, а также формирует общее соединение со всеми портами модуля, которые создают экземпляры интерфейса, необходимо объявление порта интерфейса. Разница между сетями и переменными в порт листе интерфейса и других сетях и переменных внутри интерфейса - в том, что только те, что в порт листе, могут быть соединены наружу по имени или позиции, в которой создан интерфейс. Синтаксис и семантика объявления порта интерфейса :

```systemverilog
interface i1(input a, output b, inout c);
	wire d;
endinterface
```

Провода `a`, `b`, и `c` могут быть подсоединены к интерфейсу и, следовательно, совместно использоваться другими интерфейсами.

Следующий пример показывает как определять интерфейсы со входами, позволяющими проводам совместно использоваться между двумя экземплярами интерфейса:

```systemverilog
interface simple_bus(input logic clk);  // define the interface
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;
endinterface: simple_bus

module memMod(simple_bus a);  // uses just the interface
	logic avail;

	always @(posedge a.clk)  // the clk signal from the interface
		a.gnt <= a.req & avail;
endmodule

module top;
	logic clk = 0;

	simple_bus sb_intf1(clk);  // instantiate the interface
	simple_bus sb_intf2(clk);  // instantiate the interface

	memMod mem1(.a(sb_intf1));  // reference simple_bus 1 to memory 1
	cpuMod cpu1(.b(sb_intf1));
	memMod mem2(.a(sb_intf2));  // reference simple_bus 2 to memory 1
	cpuMod cpu2(.b(sb_intf2));
endmodule
```

В предшествующем примере, созданные имена интерфейсов не совпадают с именами интерфейсов, использованных в модулях, следовательно, неявные соединения портов не могут быть использованы.

## 25.5 Modports

Чтобы ограничить доступ к интерфейсу внутри модуля, существует списки `modport` с объявленными направлениями внутри интерфейса. Ключевое слово `modport` обозначает направления, если оно существует внутри модуля.

```systemverilog
interface i2;
	wire a, b, c, d;
	modport master(input a, b, output c, d);
	modport slave(output a, b, input c, d);
endinterface
```

В этом примере, список имен `modport` может быть указан в заголовке модуля, где указывается имя интерфейса и имя модпорта выбирает соответствующую информацию о направлении для доступа к сигналам интерфейса.

```systemverilog
module m(i2.master i);
	...
endmodule

module s(i2.slave i);
	...
endmodule

module top;
	i2 i();

	m u1(.i(i));
	s u2(.i(i));
endmodule
```

Синтаксис `interface_name.modport_name reference_name` дает локальное имя для иерархической ссылки. Этот метод может обобщить на любой интерфейс с заданным именем модпорта, записав `interface.modport_name reference_name`.

Список имен `modport` (`master` или `slave`) также может быть определен при соединении портов в экземпляре модуля, в таком случае имя модпарта - иерархически исходит от экземпляра интерфейса.

```systemverilog
module m(i2 i);
	...
endmodule

module s(i2 i);
	...
endmodule

module top;
	i2 i();

	m u1(.i(i.master));
	s u2(.i(i.slave));
endmodule
```

Если соединение портов определяет имя списка `modport` в обоих местах, в экземпляре модуля и в заголовке объявления, тогда `modport` должен быть указан один и тот же.

Все имена, используемые в модпортах должны быть объявлены в том же интерфейсе, в котором объявлен сам модпорт. В частности, используемые имена не могут быть объявлены другим включенным интерфейсом, а также модпорт не может неявно объявлять новые порты.

Следующее объявление интерфейса не корректно:

```systemverilog
interface i;
	wire x, y;

	interface illegal_i;
		wire a, b, c, d;
		// x, y not declared by this interface
		modport master(input a, b, x, output c, d, y);
		modport slave(output a, b, x, input c, d, y);
	endinterface: illegal_i
endinterface: i

interface illegal_i;
	// a, b, c, d not declared by this interface
	modport master(input a, b, output c, d);
	modport slave(output a, b, input c, d);
endinterface: illegal_i
```

### 25.5.1 Example of named port bundle

Этот пример показывает, как использовать модпорты, чтобы управлять направлениями сигналов при объявлении портов. В нем используется име модпорта в объявлении модуля.

```systemverilog
interface simple_bus(input logic clk);  // define the interface
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;

	modport slave(input req, addr, mode, start, clk,
				  output gnt, rdy,
				  ref data);

	modport master(input gnt, rdy, clk,
				   output req, addr, mode, start,
				   ref data);

endinterface: simple_bus

module memMod(simple_bus.slave a);  // if name and modport name
	logic avail;
	always @(posedge a.clk)
		a.gnt <= a.req & avail;
endmodule

module cpuMod(simple_bus.master b);
	...
endmodule

module top;
	logic clk = 0;
	simple_bus sb_intf(clk);  // instantiate the interface

	initial repeat(10) #10 clk++;

	memMod mem(.a(sb_intf));  // connect the if to the module inst
	cpuMod cpu(.b(sb_intf));
endmodule
```

# 25.5.2 Example of connecting port bundle

Этот пример показывает как использовать модпорты для ограничения доступа к сигналам интерфейса и для управления их направлений. В примере имя модпорта используется в объявлении модуля.

```systemverilog
interface simple_bus(input logic clk);  // define the interface
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;

	modport slave(input req, addr, mode, start, clk,
				  output gnt, rdy,
				  ref data);

	modport master(input gnt, rdy, clk,
				   output req, addr, mode, start,
				   ref data);

endinterface: simple_bus

module memMod(simple_bus a);  // uses just the interface name
	logic avail;
	always @(posedge a.clk)  // the clk signal from the interface
		a.gnt <= a.req & avail;  // the gnt and req signal in the interface
endmodule

module cpuMod(simple_bus b);
	// ...
endmodule

module top;
	logic clk = 0;
	simple_bus sb_intf(clk);  // instantiate the interface

	initial repeat(10) #10 clk++;

	memMod mem(sb_intf.slave);  // connect the modport to the module instance
	cpuMod cpu(sb_intf.master);
endmodule
```

### 25.5.3 Example of connecting port bundle to generic interface

Этот пример показывает как использовать модпорты, чтобы управлять направлениями сигналов. Он демонстрирует использование ключевого слова `interface` в определении модуля. Фактический интерфейс и режим порта определены в объявлении модуля.

```systemverilog
interface simple_bus(input logic clk);  // define the interface
	logic req, gnt;
	logic[7:0] addr, data;
	logic[1:0] mode;
	logic start, rdy;

	modport slave(input req, addr, mode, start, clk,
				  output gnt, rdy,
				  ref data);

	modport master(input gnt, rdy, clk,
				   output req, addr, mode, start,
				   ref data);

endinterface: simple_bus

module memMod(interface a);  // uses just the interface
	logic avail;
	always @(posedge a.clk)  // the clk signal from the interface
		a.gnt <= a.req & avail;  // the gnt and req signal in the interface
endmodule

module cpuMod(interface b);
	// ...
endmodule

module top;
	logic clk = 0;
	simple_bus sb_intf(clk);  // instantiate the interface

	memMod mem(sb_intf.slave);  // connect the modport to the module instance
	cpuMod cpu(sb_intf.master);
endmodule
```

### 25.5.4 Modport expressions

Выражение модпорт позволяет включать в него список элементы массивов и структур, объединения элементов и выражения шаблонов присваивания элементов, объявленных в интерфейсе. Такие выражения явно именованы с идентификатором порта и видимы только через соединение с модпортом.

Как явно именованные порты в объявлении портов модуля, идентификаторы портов существуют в собственном пространстве имен для каждого списка модпорта. Когда элемент модпорта - простой идентификатор порта, этот идентификатор используется как ссылка на элемент интерфейса, так и как ссылка на идентификатор порта. Идентификатор порта объявляется единожды, далее не должно быть других определений с таким же именем.

Например:

```systemverilog
interface I;
	logic[7:0] r;
	const int x = 1;
	bit R;
	modport A(output .P(r[3:0]), input .Q(x), R);
	modport B(output .P(r[7:4]), input .Q(2), R);
endinterface

module M(interface i);
	initial i.P = i.Q;
endmodule

module top;
	I i1();
	M u1(i1.A);
	M u2(i1.B);
	initial #1 $display("%b", i1.r);
endmodule
```

Самоопределяемый тип выражения порта становится типом для порта. Выражение для порта не должно рассматриваться в контексте присваивания. В предыдущем примере, порт Q не может быть выходом или `inout`, по причине того, что выражение для порта - константа. Выражение для порта опционально, потому что порты могут быть определены так, что не соединены с каким-либо внутренним портом.